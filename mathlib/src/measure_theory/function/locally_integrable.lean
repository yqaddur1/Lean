/-
Copyright (c) 2022 Floris van Doorn. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Floris van Doorn
-/
import measure_theory.integral.integrable_on

/-!
# Locally integrable functions

A function is called *locally integrable* (`measure_theory.locally_integrable`) if it is integrable
on every compact subset of its domain.

This file contains properties of locally integrable functions and of integrability results
on compact sets.

## Main statements

* `continuous.locally_integrable`: A continuous function is locally integrable.

-/

open measure_theory measure_theory.measure set function topological_space
open_locale topological_space interval

variables {X Y E : Type*} [measurable_space X] [topological_space X]
variables [measurable_space Y] [topological_space Y]
variables [normed_group E] {f : X ‚Üí E} {Œº : measure X}

namespace measure_theory

/-- A function `f : X ‚Üí E` is locally integrable if it is integrable on all compact sets.
  See `measure_theory.locally_integrable_iff` for the justification of this name. -/
def locally_integrable (f : X ‚Üí E) (Œº : measure X . volume_tac) : Prop :=
‚àÄ ‚¶ÉK‚¶Ñ, is_compact K ‚Üí integrable_on f K Œº

lemma integrable.locally_integrable (hf : integrable f Œº) : locally_integrable f Œº :=
Œª K hK, hf.integrable_on

lemma locally_integrable.ae_strongly_measurable [sigma_compact_space X]
  (hf : locally_integrable f Œº) :
  ae_strongly_measurable f Œº :=
begin
  rw [‚Üê @restrict_univ _ _ Œº, ‚Üê Union_compact_covering, ae_strongly_measurable_Union_iff],
  exact Œª i, (hf $ is_compact_compact_covering X i).ae_strongly_measurable
end

lemma locally_integrable_iff [locally_compact_space X] :
  locally_integrable f Œº ‚Üî ‚àÄ x : X, ‚àÉ U ‚àà ùìù x, integrable_on f U Œº :=
begin
  refine ‚ü®Œª hf x, _, Œª hf K hK, _‚ü©,
  { obtain ‚ü®K, hK, h2K‚ü© := exists_compact_mem_nhds x, exact ‚ü®K, h2K, hf hK‚ü© },
  { refine is_compact.induction_on hK integrable_on_empty (Œª s t hst h, h.mono_set hst)
      (Œª s t hs ht, integrable_on_union.mpr ‚ü®hs, ht‚ü©) (Œª x hx, _),
    obtain ‚ü®K, hK, h2K‚ü© := hf x,
    exact ‚ü®K, nhds_within_le_nhds hK, h2K‚ü© }
end

section real
variables [opens_measurable_space X] {A K : set X} {g g' : X ‚Üí ‚Ñù}

lemma integrable_on.mul_continuous_on_of_subset
  (hg : integrable_on g A Œº) (hg' : continuous_on g' K)
  (hA : measurable_set A) (hK : is_compact K) (hAK : A ‚äÜ K) :
  integrable_on (Œª x, g x * g' x) A Œº :=
begin
  rcases is_compact.exists_bound_of_continuous_on hK hg' with ‚ü®C, hC‚ü©,
  rw [integrable_on, ‚Üê mem_‚Ñíp_one_iff_integrable] at hg ‚ä¢,
  have : ‚àÄ·µê x ‚àÇ(Œº.restrict A), ‚à•g x * g' x‚à• ‚â§ C * ‚à•g x‚à•,
  { filter_upwards [ae_restrict_mem hA] with x hx,
    rw [real.norm_eq_abs, abs_mul, mul_comm, real.norm_eq_abs],
    apply mul_le_mul_of_nonneg_right (hC x (hAK hx)) (abs_nonneg _), },
  exact mem_‚Ñíp.of_le_mul hg (hg.ae_strongly_measurable.ae_measurable.mul
    ((hg'.mono hAK).ae_measurable hA)).ae_strongly_measurable this,
end

lemma integrable_on.mul_continuous_on [t2_space X]
  (hg : integrable_on g K Œº) (hg' : continuous_on g' K) (hK : is_compact K) :
  integrable_on (Œª x, g x * g' x) K Œº :=
hg.mul_continuous_on_of_subset hg' hK.measurable_set hK (subset.refl _)

lemma integrable_on.continuous_on_mul_of_subset
  (hg : continuous_on g K) (hg' : integrable_on g' A Œº)
  (hK : is_compact K) (hA : measurable_set A) (hAK : A ‚äÜ K) :
  integrable_on (Œª x, g x * g' x) A Œº :=
by simpa [mul_comm] using hg'.mul_continuous_on_of_subset hg hA hK hAK

lemma integrable_on.continuous_on_mul [t2_space X]
  (hg : continuous_on g K) (hg' : integrable_on g' K Œº) (hK : is_compact K) :
  integrable_on (Œª x, g x * g' x) K Œº :=
integrable_on.continuous_on_mul_of_subset hg hg' hK hK.measurable_set subset.rfl

end real

end measure_theory
open measure_theory

/-- If a function is integrable at `ùìù[s] x` for each point `x` of a compact set `s`, then it is
integrable on `s`. -/
lemma is_compact.integrable_on_of_nhds_within {K : set X} (hK : is_compact K)
  (hf : ‚àÄ x ‚àà K, integrable_at_filter f (ùìù[K] x) Œº) : integrable_on f K Œº :=
is_compact.induction_on hK integrable_on_empty (Œª s t hst ht, ht.mono_set hst)
  (Œª s t hs ht, hs.union ht) hf

section borel

variables [opens_measurable_space X] [metrizable_space X] [is_locally_finite_measure Œº]
variables {K : set X} {a b : X}

/-- A function `f` continuous on a compact set `K` is integrable on this set with respect to any
locally finite measure. -/
lemma continuous_on.integrable_on_compact (hK : is_compact K) (hf : continuous_on f K) :
  integrable_on f K Œº :=
begin
  letI := metrizable_space_metric X,
  apply hK.integrable_on_of_nhds_within (Œª x hx, _),
  exact hf.integrable_at_nhds_within_of_is_separable hK.measurable_set hK.is_separable hx,
end

/-- A continuous function `f` is locally integrable with respect to any locally finite measure. -/
lemma continuous.locally_integrable (hf : continuous f) : locally_integrable f Œº :=
Œª s hs, hf.continuous_on.integrable_on_compact hs

lemma continuous_on.integrable_on_Icc [preorder X] [compact_Icc_space X]
  (hf : continuous_on f (Icc a b)) : integrable_on f (Icc a b) Œº :=
hf.integrable_on_compact is_compact_Icc

lemma continuous.integrable_on_Icc [preorder X] [compact_Icc_space X] (hf : continuous f) :
  integrable_on f (Icc a b) Œº :=
hf.locally_integrable is_compact_Icc

lemma continuous.integrable_on_Ioc [preorder X] [compact_Icc_space X] (hf : continuous f) :
  integrable_on f (Ioc a b) Œº :=
hf.integrable_on_Icc.mono_set Ioc_subset_Icc_self

lemma continuous_on.integrable_on_interval [linear_order X] [compact_Icc_space X]
  (hf : continuous_on f [a, b]) : integrable_on f [a, b] Œº :=
hf.integrable_on_Icc

lemma continuous.integrable_on_interval [linear_order X] [compact_Icc_space X] (hf : continuous f) :
  integrable_on f [a, b] Œº :=
hf.integrable_on_Icc

lemma continuous.integrable_on_interval_oc [linear_order X] [compact_Icc_space X]
  (hf : continuous f) : integrable_on f (Œô a b) Œº :=
hf.integrable_on_Ioc

/-- A continuous function with compact support is integrable on the whole space. -/
lemma continuous.integrable_of_has_compact_support
  (hf : continuous f) (hcf : has_compact_support f) : integrable f Œº :=
(integrable_on_iff_integable_of_support_subset (subset_tsupport f) measurable_set_closure).mp $
  hf.locally_integrable hcf

end borel

section monotone

variables [borel_space X] [metrizable_space X]
  [conditionally_complete_linear_order X] [conditionally_complete_linear_order E]
  [order_topology X] [order_topology E] [second_countable_topology E]
  [is_locally_finite_measure Œº] {s : set X}

lemma monotone_on.integrable_on_compact (hs : is_compact s) (hmono : monotone_on f s) :
  integrable_on f s Œº :=
begin
  borelize E,
  obtain rfl | h := s.eq_empty_or_nonempty,
  { exact integrable_on_empty },
  have hbelow : bdd_below (f '' s) :=
    ‚ü®f (Inf s), Œª x ‚ü®y, hy, hyx‚ü©, hyx ‚ñ∏ hmono (hs.Inf_mem h) hy (cInf_le hs.bdd_below hy)‚ü©,
  have habove : bdd_above (f '' s) :=
    ‚ü®f (Sup s), Œª x ‚ü®y, hy, hyx‚ü©, hyx ‚ñ∏ hmono hy (hs.Sup_mem h) (le_cSup hs.bdd_above hy)‚ü©,
  have : metric.bounded (f '' s) := metric.bounded_of_bdd_above_of_bdd_below habove hbelow,
  rcases bounded_iff_forall_norm_le.mp this with ‚ü®C, hC‚ü©,
  refine integrable.mono' (continuous_const.locally_integrable hs)
    (ae_measurable_restrict_of_monotone_on hs.measurable_set hmono).ae_strongly_measurable
    ((ae_restrict_iff' hs.measurable_set).mpr $ ae_of_all _ $
      Œª y hy, hC (f y) (mem_image_of_mem f hy)),
end

lemma antitone_on.integrable_on_compact (hs : is_compact s) (hanti : antitone_on f s) :
  integrable_on f s Œº :=
hanti.dual_right.integrable_on_compact hs

lemma monotone.locally_integrable (hmono : monotone f) : locally_integrable f Œº :=
Œª s hs, (hmono.monotone_on _).integrable_on_compact hs

lemma antitone.locally_integrable (hanti : antitone f) : locally_integrable f Œº :=
hanti.dual_right.locally_integrable

end monotone

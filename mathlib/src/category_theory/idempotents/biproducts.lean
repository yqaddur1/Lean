/-
Copyright (c) 2022 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/

import category_theory.idempotents.karoubi
import category_theory.additive.basic

/-!

# Biproducts in the idempotent completion of a preadditive category

In this file, we define an instance expressing that if `C` is an additive category,
then `karoubi C` is also an additive category.

We also obtain that for all `P : karoubi C` where `C` is a preadditive category `C`, there
is a canonical isomorphism `P ‚äû P.complement ‚âÖ (to_karoubi C).obj P.X` in the category
`karoubi C` where `P.complement` is the formal direct factor of `P.X` corresponding to
the idempotent endomorphism `ùüô P.X - P.p`.

-/

noncomputable theory

open category_theory.category
open category_theory.limits
open category_theory.preadditive

universes v

namespace category_theory

namespace idempotents

namespace karoubi

variables {C : Type*} [category.{v} C] [preadditive C]

namespace biproducts

/-- The `bicone` used in order to obtain the existence of
the biproduct of a functor `J ‚•§ karoubi C` when the category `C` is additive. -/
@[simps]
def bicone [has_finite_biproducts C] {J : Type v} [decidable_eq J] [fintype J]
  (F : J ‚Üí karoubi C) : bicone F :=
{ X :=
  { X := biproduct (Œª j, (F j).X),
    p := biproduct.map (Œª j, (F j).p),
    idem := begin
      ext j,
      simp only [biproduct.Œπ_map_assoc, biproduct.Œπ_map],
      slice_lhs 1 2 { rw (F j).idem, },
    end, },
  œÄ := Œª j,
    { f := biproduct.map (Œª j, (F j).p) ‚â´ bicone.œÄ _ j,
      comm := by simp only [assoc, biproduct.bicone_œÄ, biproduct.map_œÄ,
        biproduct.map_œÄ_assoc, (F j).idem], },
  Œπ := Œª j,
    { f := (by exact bicone.Œπ _ j) ‚â´ biproduct.map (Œª j, (F j).p),
      comm := by rw [biproduct.Œπ_map, ‚Üê assoc, ‚Üê assoc, (F j).idem,
        assoc, biproduct.Œπ_map, ‚Üê assoc, (F j).idem], },
  Œπ_œÄ := Œª j j', begin
    split_ifs,
    { subst h,
      simp only [biproduct.bicone_Œπ, biproduct.Œπ_map, biproduct.bicone_œÄ,
        biproduct.Œπ_œÄ_self_assoc, comp, category.assoc, eq_to_hom_refl, id_eq,
        biproduct.map_œÄ, (F j).idem], },
    { simpa only [hom_ext, biproduct.Œπ_œÄ_ne_assoc _ h, assoc,
        biproduct.map_œÄ, biproduct.map_œÄ_assoc, zero_comp, comp], },
  end, }

end biproducts

lemma karoubi_has_finite_biproducts [has_finite_biproducts C] :
  has_finite_biproducts (karoubi C) :=
{ has_biproducts_of_shape := Œª J hJ‚ÇÅ hJ‚ÇÇ,
  { has_biproduct := Œª F, begin
      letI := hJ‚ÇÇ,
      apply has_biproduct_of_total (biproducts.bicone F),
      ext1, ext1,
      simp only [id_eq, comp_id, biproducts.bicone_X_p, biproduct.Œπ_map],
      rw [sum_hom, comp_sum, finset.sum_eq_single j], rotate,
      { intros j' h1 h2,
        simp only [biproduct.Œπ_map, biproducts.bicone_Œπ_f, biproducts.bicone_œÄ_f,
          assoc, comp, biproduct.map_œÄ],
        slice_lhs 1 2 { rw biproduct.Œπ_œÄ, },
        split_ifs,
        { exfalso, exact h2 h.symm, },
        { simp only [zero_comp], } },
      { intro h,
        exfalso,
        simpa only [finset.mem_univ, not_true] using h, },
      { simp only [biproducts.bicone_œÄ_f, comp,
          biproduct.Œπ_map, assoc, biproducts.bicone_Œπ_f, biproduct.map_œÄ],
        slice_lhs 1 2 { rw biproduct.Œπ_œÄ, },
        split_ifs, swap, { exfalso, exact h rfl, },
        simp only [eq_to_hom_refl, id_comp, (F j).idem], },
    end, } }

instance {D : Type*} [category D] [additive_category D] : additive_category (karoubi D) :=
{ to_preadditive := infer_instance,
  to_has_finite_biproducts := karoubi_has_finite_biproducts }

/-- `P.complement` is the formal direct factor of `P.X` given by the idempotent
endomorphism `ùüô P.X - P.p` -/
@[simps]
def complement (P : karoubi C) : karoubi C :=
{ X := P.X,
  p := ùüô _ - P.p,
  idem := idem_of_id_sub_idem P.p P.idem, }

instance (P : karoubi C) : has_binary_biproduct P P.complement :=
has_binary_biproduct_of_total
{ X := P.X,
  fst := P.decomp_id_p,
  snd := P.complement.decomp_id_p,
  inl := P.decomp_id_i,
  inr := P.complement.decomp_id_i,
  inl_fst' := P.decomp_id.symm,
  inl_snd' := begin
    simp only [decomp_id_i_f, decomp_id_p_f, complement_p, comp_sub, comp,
      hom_ext, quiver.hom.add_comm_group_zero_f, P.idem],
    erw [comp_id, sub_self],
  end,
  inr_fst' := begin
    simp only [decomp_id_i_f, complement_p, decomp_id_p_f, sub_comp, comp,
      hom_ext, quiver.hom.add_comm_group_zero_f, P.idem],
    erw [id_comp, sub_self],
  end,
  inr_snd' := P.complement.decomp_id.symm, }
(by simp only [hom_ext, ‚Üê decomp_p, quiver.hom.add_comm_group_add_f,
  to_karoubi_map_f, id_eq, coe_p, complement_p, add_sub_cancel'_right])

/-- A formal direct factor `P : karoubi C` of an object `P.X : C` in a
preadditive category is actually a direct factor of the image `(to_karoubi C).obj P.X`
of `P.X` in the category `karoubi C` -/
def decomposition (P : karoubi C) : P ‚äû P.complement ‚âÖ (to_karoubi _).obj P.X :=
{ hom := biprod.desc P.decomp_id_i P.complement.decomp_id_i,
  inv := biprod.lift P.decomp_id_p P.complement.decomp_id_p,
  hom_inv_id' := begin
    ext1,
    { simp only [‚Üê assoc, biprod.inl_desc, comp_id, biprod.lift_eq, comp_add,
        ‚Üê decomp_id, id_comp, add_right_eq_self],
      convert zero_comp,
      ext,
      simp only [decomp_id_i_f, decomp_id_p_f, complement_p, comp_sub, comp,
        quiver.hom.add_comm_group_zero_f, P.idem],
      erw [comp_id, sub_self], },
    { simp only [‚Üê assoc, biprod.inr_desc, biprod.lift_eq, comp_add,
        ‚Üê decomp_id, comp_id, id_comp, add_left_eq_self],
      convert zero_comp,
      ext,
      simp only [decomp_id_i_f, decomp_id_p_f, complement_p, sub_comp, comp,
        quiver.hom.add_comm_group_zero_f, P.idem],
      erw [id_comp, sub_self], }
  end,
  inv_hom_id' := begin
    rw biprod.lift_desc,
    simp only [‚Üê decomp_p],
    ext,
    dsimp only [complement, to_karoubi],
    simp only [quiver.hom.add_comm_group_add_f, add_sub_cancel'_right, id_eq],
  end, }

end karoubi

end idempotents

end category_theory

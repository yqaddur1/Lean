/-
Copyright (c) 2022 Pierre-Alexandre Bazin. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Pierre-Alexandre Bazin
-/
import analysis.seminorm
/-!
# The lattice of seminorms is not distributive

We provide an example of three seminorms over the ‚Ñù-vector space ‚Ñù√ó‚Ñù which don't satisfy the lattice
distributivity property `(p ‚äî q1) ‚äì (p ‚äî q2) ‚â§ p ‚äî (q1 ‚äì q2)`.

This proves the lattice `seminorm ‚Ñù (‚Ñù √ó ‚Ñù)` is not distributive.

## References

* https://en.wikipedia.org/wiki/Seminorm#Examples
-/

namespace seminorm_not_distrib
open_locale nnreal

private lemma bdd_below_range_add {ùïú E : Type*} [normed_field ùïú] [add_comm_group E] [module ùïú E]
  (x : E) (p q : seminorm ùïú E) :
  bdd_below (set.range (Œª (u : E), p u + q (x - u))) :=
by { use 0, rintro _ ‚ü®x, rfl‚ü©, exact add_nonneg (p.nonneg _) (q.nonneg _) }

@[simps] noncomputable def p : seminorm ‚Ñù (‚Ñù√ó‚Ñù) :=
(norm_seminorm ‚Ñù ‚Ñù).comp (linear_map.fst _ _ _) ‚äî (norm_seminorm ‚Ñù ‚Ñù).comp (linear_map.snd _ _ _)

@[simps] noncomputable def q1 : seminorm ‚Ñù (‚Ñù√ó‚Ñù) :=
(4 : ‚Ñù‚â•0) ‚Ä¢ (norm_seminorm ‚Ñù ‚Ñù).comp (linear_map.fst _ _ _)

@[simps] noncomputable def q2 : seminorm ‚Ñù (‚Ñù√ó‚Ñù) :=
(4 : ‚Ñù‚â•0) ‚Ä¢ (norm_seminorm ‚Ñù ‚Ñù).comp (linear_map.snd _ _ _)

lemma eq_one : (p ‚äî (q1 ‚äì q2)) (1, 1) = 1 :=
begin
  dsimp [-seminorm.inf_apply],
  rw [sup_idem, norm_one, sup_eq_left],
  apply cinfi_le_of_le (bdd_below_range_add _ _ _) ((0, 1) : ‚Ñù√ó‚Ñù), dsimp,
  simp only [norm_zero, smul_zero, sub_self, add_zero, zero_le_one]
end

/-- This is a counterexample to the distributivity of the lattice `seminorm ‚Ñù (‚Ñù √ó ‚Ñù)`. -/
lemma not_distrib : ¬¨((p ‚äî q1) ‚äì (p ‚äî q2) ‚â§ p ‚äî (q1 ‚äì q2)) :=
begin
  intro le_sup_inf,
  have c : ¬¨(4/3 ‚â§ (1:‚Ñù)) := by norm_num,
  apply c, nth_rewrite 2 ‚Üê eq_one,
  apply le_trans _ (le_sup_inf _),
  apply le_cinfi, intro x,
  cases le_or_lt x.fst (1/3) with h1 h1,
  { cases le_or_lt x.snd (2/3) with h2 h2,
    { calc 4/3 = 4 * (1 - 2/3) : by norm_num
           ... ‚â§ 4 * (1 - x.snd) : (mul_le_mul_left zero_lt_four).mpr (sub_le_sub_left h2 _)
           ... ‚â§ 4 * |1 - x.snd| : (mul_le_mul_left zero_lt_four).mpr (le_abs_self _)
           ... = q2 ((1, 1) - x) : rfl
           ... ‚â§ (p ‚äî q2) ((1, 1) - x) : le_sup_right
           ... ‚â§ (p ‚äî q1) x + (p ‚äî q2) ((1, 1) - x) : le_add_of_nonneg_left ((p ‚äî q1).nonneg _) },
    { calc 4/3 = 2/3 + (1 - 1/3) : by norm_num
           ... ‚â§ x.snd + (1 - x.fst) : add_le_add (le_of_lt h2) (sub_le_sub_left h1 _)
           ... ‚â§ |x.snd| + |1 - x.fst| : add_le_add (le_abs_self _) (le_abs_self _)
           ... ‚â§ p x + p ((1, 1) - x) : add_le_add le_sup_right le_sup_left
           ... ‚â§ (p ‚äî q1) x + (p ‚äî q2) ((1, 1) - x) : add_le_add le_sup_left le_sup_left } },
  { calc 4/3 = 4 * (1/3) : by norm_num
         ... ‚â§ 4 * x.fst : (mul_le_mul_left zero_lt_four).mpr (le_of_lt h1)
         ... ‚â§ 4 * |x.fst| : (mul_le_mul_left zero_lt_four).mpr (le_abs_self _)
         ... = q1 x : rfl
         ... ‚â§ (p ‚äî q1) x : le_sup_right
         ... ‚â§ (p ‚äî q1) x + (p ‚äî q2) ((1, 1) - x) : le_add_of_nonneg_right ((p ‚äî q2).nonneg _) }
end

end seminorm_not_distrib
